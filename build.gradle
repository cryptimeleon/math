plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.cryptimeleon'
archivesBaseName = project.name
boolean isRelease = project.hasProperty("release")
version = '1.0.0'  + (isRelease ? "" : "-SNAPSHOT")

description = 'The Math library provides the mathematical foundation for the other Cryptimeleon libraries. ' +
        'It provides basics such as mathematical groups, rings and fields, e.g. Z_n , ' +
        'as well as implementations of cryptographic pairings.'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}



repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version:'1.1'
    implementation group: 'org.reflections', name: 'reflections', version:'0.9.10'
    
    // For tests
    testCompileOnly(
            'junit:junit:4.12'
    )
    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.1.0',
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.1.0',
            'org.junit.vintage:junit-vintage-engine:5.1.0'
    )
}


test {
    useJUnitPlatform()
    maxParallelForks 4
    //we want display the following test events
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}

task javadocLatex(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    options.addBooleanOption("-allow-script-in-comments", true)
    options.header = "<script type\"text/javascript&\" src=\"" +
            "https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.7/MathJax.js?" +
            "config=TeX-MML-AM_CHTML\"></script>"
    // reduce javadoc linting
    options.addBooleanOption('Xdoclint:none', true)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadocLatex) {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

java {
    registerFeature("tests") {
        usingSourceSet(sourceSets.test)
    }
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            artifacts {
                archives javadocJar, sourcesJar
            }

            pom {
                name = 'Math'
                url = 'https://cryptimeleon.org'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jbobolz'
                        name = 'Jan Bobolz'
                        email = 'mail@jan-bobolz.de'
                        organization = 'Paderborn University'
                    }
                    developer {
                        id = 'feidens'
                        name = 'Fabian Eidens'
                        email = 'fabianeidens@gmail.com'
                        organization = 'Paderborn University'
                        url = 'https://feidens.github.io/'
                    }
                    developer {
                        id = 'rheitjoh'
                        name = 'Raphael Heitjohann'
                        email = 'rheitjoh@mail.uni-paderborn.de'
                        organization = 'Paderborn University'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cryptimeleon/math.git'
                    developerConnection = 'scm:git:https://github.com/cryptimeleon/math.git'
                    url = 'https://github.com/cryptimeleon/math/'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_TOKEN")
            }
            name = 'OSSRH'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            url = version.endsWith('SNAPSHOT') ? '' : releasesRepoUrl
        }
    }
}

signing {
    required(project.hasProperty("release"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
